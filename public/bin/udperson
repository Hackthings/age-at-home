#!/bin/tcsh
if ($#argv > 0) then
    set TTL = $argv[1]
else
    set TTL = 1800
endif
# get time
set SECONDS = `date "+%s"`
set TTLSECS = `echo $SECONDS \/ $TTL \* $TTL | bc`

set class = 'person'

set INPUT = "rough-fog.$TTLSECS.json"
if ( ! -e "$INPUT" ) then
    echo "STAT: creating $INPUT"
    /bin/rm -f "$INPUT:r:r".[0-9]*".json"
    /bin/rm -f "rough-fog.json"
    $0:h/getjson2csv rough-fog $TTL none
    /bin/mv "rough-fog.json" "$INPUT"
    /bin/cp "$INPUT" "rough-fog.json"
endif

set OUTPUT = "rough-fog-$class.$TTLSECS.csv"
if ( ! -e "$OUTPUT") then
    echo "STAT: creating $OUTPUT"
    /bin/rm -f "$OUTPUT:r:r".[0-9]*".csv"
    $0:h/mkclassvalues rough-fog $class
    $0:h/mkintervals rough-fog
    csvgrep -c classifier -m "$class" rough-fog-$class-values.csv | csvjoin -c "id,id" - rough-fog-intervals.csv | csvcut -c "interval,day,week,classifier,score" >! "$OUTPUT"
    /bin/rm -f "rough-fog-$class.csv"
    /bin/cp "$OUTPUT" "rough-fog-$class.csv"
endif
echo "STAT: $OUTPUT:r:r.csv is current with TTL of $TTL"

set days = ( Sunday Monday Tuesday Wednesday Thursday Friday Saturday )
# set intervals = `tail +2 rough-fog-$class.csv | awk -F, '{ print $1 }' | sort -n | uniq`
set intervals = ()
@ i = 0
# there are 96 15 minute intervals per day
while ($i < 96)
    set intervals = ( $intervals $i )
    @ i++
end
set intnames = `echo $intervals | sed "s/ /,/g"`

set output = "$class.csv"

echo "day,weeks,$intnames" >! "$output"
foreach d ( $days )
    set nweek = `csvgrep -c day -m "$d" rough-fog-$class.csv | csvcut -c week | tail +2 | sort | uniq | wc -l`
    echo -n "$d [$nweek]: "
    echo -n "$d,$nweek," >> "$output"
    @ j = 1
    foreach i ( $intervals )
	if ($j > 1) echo -n "," >> "$output"
	set k = `egrep "^$i,$d," rough-fog-$class.csv | wc -l | awk '{ print $1 }'`
	echo -n "$k "
	echo -n "$k" >> "$output"
	@ j++
    end
    echo
    echo  >> "$output"
end
