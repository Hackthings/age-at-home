#!/bin/csh

if ($?CLOUDANT_DATABASE) then
    setenv DB "$CLOUDANT_DATABASE"
else
    setenv DB "rough-fog"
endif

if ( $#argv > 0 ) then
    setenv ALLROWS "$argv[1]".csv
else
    setenv ALLROWS "$DB".csv
endif

if ( -e "$ALLROWS" ) then
    setenv ARBASE "$ALLROWS:r"

    if ( ! -e "$ARBASE-classes.txt" ) then
	echo "STAT: making $ARBASE-classes.txt"

	set vcolset = `csvstat -n "$ALLROWS" | awk '{ print $2 }' | egrep classifier_id`
	set acolset = `csvstat -n "$ALLROWS" | awk '{ print $2 }' | egrep alchemy/text`

	foreach i ( $acolset )
	    csvcut -c "$i" "$ALLROWS" | tail +2 >>! "$ARBASE"-classes.txt
	    cat "$ARBASE"-classes.txt | sort | uniq >! tmp.$$
	    mv -f tmp.$$ "$ARBASE"-classes.txt
	end
	foreach i ( $vcolset )
	    csvcut -c "$i" "$ALLROWS" | tail +2 >>! "$ARBASE"-classes.txt
	    cat "$ARBASE"-classes.txt | sort | uniq >! tmp.$$
	    mv -f tmp.$$ "$ARBASE"-classes.txt
	end
    endif
else
    exit
endif

set classifiers = ( `cat "$ARBASE"-classes.txt` )

if ($#classifiers > 1) then
    echo "STAT: total $#classifiers"
    set colset = `csvstat -n "$ALLROWS" | awk '{ print $2 }'`
    set colnam = `echo $colset | sed "s/ /,/g"`

    set datetime = ( doc/year doc/month doc/day doc/hour doc/minute doc/second )
    set dtcolumns = `echo "$datetime" | sed "s/ /,/g"`

    echo "classifier,score,id,$dtcolumns" >! "$ARBASE-class-values.csv"
    foreach i ( $classifiers )
	if ($i == \"\") continue

	echo -n "STAT: $i"
	echo "$colnam" >! "$i.csv"
	egrep ",$i," "$ALLROWS" >> "$i.csv"

	foreach j ( $acolset )
	    csvcut -c "$j","$j:h/score","id","$dtcolumns" "$i.csv" | egrep "^$i," >> "$ARBASE-class-values.csv"
	    echo -n "."
	end

	foreach j ( $vcolset )
	    csvcut -c "$j","$j:h/score","id","$dtcolumns" "$i.csv" | egrep "^$i," >> "$ARBASE-class-values.csv"
	    echo -n "."
	end

	echo 
	rm "$i.csv"
    end
endif
