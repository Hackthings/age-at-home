#!/bin/tcsh

if ($?CLOUDANT_DATABASE) then
    setenv DB "$CLOUDANT_DATABASE"
else
    setenv DB "rough-fog"
endif

if ( $#argv > 0 ) then
    setenv ALLROWS "$argv[1]".csv
else
    setenv ALLROWS "$DB".csv
endif

if ( -e "$ALLROWS" ) then
    setenv ARBASE "$ALLROWS:r"

    if ( (! -e "$ARBASE"-intervals.csv ) || ((-M "$ALLROWS") > (-M "$ARBASE-intervals.csv"))) then
	set colset = `csvstat -n "$ALLROWS" | awk '{ print $2 }'`
	set colnam = `echo $colset | sed "s/ /,/g"`
	# note insertion of "id" in middle
	set datetime = ( doc/year doc/month doc/day id doc/hour doc/minute doc/second )
	set dtcolumns = `echo "$datetime" | sed "s/ /,/g"`

	echo "STAT: creating $ARBASE-intervals.csv"
	echo "interval,ampm,week,day,id" >! "$ARBASE"-intervals.csv

	# note utilization of gawk(1)
	csvcut -c "$dtcolumns" "$ALLROWS" | tail +2 | \
	    gawk -F, '{ m=$5*60+$6; m = m / 15; t=mktime(sprintf("%4d %2d %2d %2d %2d %2d", $1, $2, $3, $5, $6, $7)); printf "%d,%s,%s,%s,%s\n", m, strftime("%p",t),strftime("%U",t),strftime("%A",t), $4 }' >> "$ARBASE"-intervals.csv
    else
    	echo "STAT: $ARBASE-intervals.csv up-to-date"
    endif
else
    echo "$ALLROWS does not exist; run getjson2csv $ALLROWS:r"
endif
