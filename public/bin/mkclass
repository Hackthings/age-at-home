#!/bin/tcsh

# get time
set TTL = `echo "12 * 60 * 60" | bc`
set SECONDS = `date "+%s"`
set TTLSECS = `echo $SECONDS \/ $TTL \* $TTL | bc`

#
# setup CLOUDANT
#
if ($?CLOUDANT_USERNAME) then
    setenv CN $CLOUDANT_USERNAME
else
    setenv CN "538e7925-b7f5-478b-bf75-2f292dcf642a-bluemix"
endif

if ($?CLOUDANT_URL) then
    setenv CU $CLOUDANT_URL
else
    if (-e ~/.cloudant_url) then
        setenv CU `cat ~/.cloudant_url`
    else if ($?CN && $?CP) then
        setenv CU "https://$CN":"$CP"@"$CN.cloudant.com"
    else if ($?CN) then
        setenv CU "https://$CN.cloudant.com"
    else
        echo "ERR: $0 - No Cloudant URL"
	exit
    endif
endif

if ($?CLOUDANT_DATABASE) then
    setenv DB $CLOUDANT_DATABASE
else
    setenv DB "rough-fog"
endif

set class = person
set classes = ( "$class" )
setenv CLASSIFIERS "$classes"

if ( $#argv > 0 ) then
    setenv DB "$argv[1]"
    if ( $#argv > 1 ) then
	set classes = "$argv[2]"
        if (-e ".classifiers-$classes.json") then
	    setenv CLASSIFIERS `jq ."$classes"\[\].name ".classifiers-$classes.json" | sed 's/"//g'`
	else if ($classes == "all" || $classes == "alchemy") then
	    if ( ! -e "$DB-$classes.txt") then
	        $0:h/mkallclass $DB $classes
	    endif
	    setenv CLASSIFIERS `cat "$DB-$classes.txt"`
        else if ($#argv > 3) then
            set classes = "misc"
            setenv CLASSIFIERS "$argv[2-]"
        else
            setenv CLASSIFIERS "$argv[2]"
        endif
    endif
else
    setenv DB "rough-fog"
endif

if ($?DB == 0) then
    setenv DB "rough-fog"
endif

# get last sequence # for class specified
set prev_seqid = `curl "$CU/rough-fog-stats/$class" | jq .seqid`
if ($prev_seqid[1] == "null") then
    set prev_seqid = 0
endif

set INPUT = "$DB.$TTLSECS.json"
if ( ! -e "$INPUT" ) then
    echo "STAT: $0 - creating $INPUT (since = $prev_seqid)"
    /bin/rm -f "$INPUT:r:r".[0-9]*".json"
    /bin/rm -f "$DB.json"
    # replace zero (0) w/ $prev_seqid when ready
    $0:h/getjson2csv $DB 0 none
    /bin/mv "$DB.json" "$INPUT"
    /bin/cp "$INPUT" "$DB.json"
    $0:h/mkintervals $DB
endif
set tintervals = `wc -l $DB-intervals.csv | awk '{ print $1-1 }' | bc`
set seqid = ( `jq .last_seq "$INPUT"` )

# setup days of week and intervals of day (15 minute increments)
set days = ( Sunday Monday Tuesday Wednesday Thursday Friday Saturday )
set intervals = ()
set intvalues = ()
@ i = 0
# there are 96 15 minute intervals per day
while ($i < 96)
    set intervals = ( $intervals $i )
    set intvalues = ( $intvalues "c$i,s$i,m$i,d$i" )
    @ i++
end
set intnames = `echo $intvalues | sed "s/ /,/g"`

#
# process all specified CLASSIFIERS
#
foreach class ( $CLASSIFIERS )
    if ($class == \"\") continue
    if ($class == "NO_TAGS") continue

    set OUTPUT = "$DB-$class-intervals.csv"
    if (! -e $OUTPUT || ((-M $INPUT) > (-M $OUTPUT))) then
	echo "STAT: $0 - creating $OUTPUT"
	$0:h/mkclassvalues $DB $class
	csvgrep -c classifier -m "$class" $DB-$class-values.csv | csvjoin -c "id,id" - $DB-intervals.csv | csvcut -c "interval,day,week,classifier,score" >! "$OUTPUT"
    endif

    set cintervals = `wc -l $DB-$class-intervals.csv | awk '{ print $1-1 }' | bc`
    set percent = `echo "$cintervals / $tintervals * 100" | bc -l`; set percent = $percent:r

    set output = "$DB-$class.csv"
    set statjson = "$output:r.json"

    if ((($cintervals >= 1) || ($class == "person")) && ((! -e $output || ! -e $statjson) || ((-M $DB-$class-intervals.csv) > (-M $output)))) then
        echo "STAT: $0 - creating $output and $statjson"
	echo "day,weeks,$intnames" >! "$output"
	echo -n '{ "seqid":'$seqid',"days":[' >! "$statjson"
	@ i = 0
	foreach d ( $days )
	    if ($i > 1) echo -n "," >> "$statjson"
	    set weeks = `csvgrep -c day -m "$d" $DB-$class-intervals.csv | csvcut -c week | tail +2 | sort -rn | uniq | awk 'BEGIN { c=0 } { if (c > 0) printf ", "; printf "\"%2d\"", $1; c++ }'`
	    echo -n '{"weekday":"'$d'","nweek":"'$#weeks'","weeks":['$weeks'],"intervals":[' >> "$statjson"
	    echo -n "$d,$#weeks," >> "$output"
	    @ j = 1
	    echo -n "$d "
	    foreach i ( $intervals )
		if ($j > 1) echo -n "," >> "$statjson"
		if ($j > 1) echo -n "," >> "$output"
		set l = `egrep "^$i,$d," $DB-$class-intervals.csv | awk -F, 'BEGIN { c=0; s=0; m=0; v=0; vs=0 } { c++; s=s+$5; m=s/c; vs=vs+($5-m)^2; v=vs/c } END { sd=sqrt(v); printf "%d %f %f %f", c, s, m, sd }'`
		echo -n '{"count":"'$l[1]'","sum":"'$l[2]'","mean":"'$l[3]'","stdev":"'$l[4]'"}' >> "$statjson"
		echo -n "$l" | sed "s/ /,/g" >> "$output"
		@ j++
		echo -n '.'
	    end
	    echo "] }" >> "$statjson"
	    echo  >> "$output"
	    @ i++
	    echo
	end
	echo "] }" >> "$statjson"
    endif

    # update Cloudant
    if ($?CLOUDANT_OFF == 0 && $?CU && $?DB) then
        echo "STAT: $0 - updating cloudant"
	set DEVICE_DB = `curl -s -q -X GET "$CU/$DB-stats" | jq '.db_name'`
	if ( "$DEVICE_DB" == "null" ) then
	    # create DB
	    set DEVICE_DB = `curl -s -q -X PUT "$CU/$DB-stats" | jq '.ok'`
	    # test for success
	    if ( "$DEVICE_DB" != "true" ) then
		# failure
		setenv CLOUDANT_OFF TRUE
	    endif
	endif
	if ( $?CLOUDANT_OFF == 0 ) then
	    set doc = ( `curl -s -q "$CU/$DB-stats/$class" | jq ._id,._rev | sed 's/"//g'` )
	    if ($#doc == 2 && $doc[1] == $class && $doc[2] != "") then
		set rev = $doc[2]
		curl -s -q -X DELETE "$CU/$DB-stats/$class?rev=$rev"
	    endif
	    if (($cintervals >= 1) || ($class == "person")) then
		echo "STAT: $0 - STORED $class ($cintervals; $percent%)"
		curl -s -q -H "Content-type: application/json" -X PUT "$CU/$DB-stats/$class" -d "@$statjson"
	    else
	        echo "STAT: $0 - DELETE $class ($cintervals; $percent%)"
	    endif
	endif
    else
	echo "STAT: $0 - No CLOUDANT defined $?CLOUDANT_OFF $CU $DB"
    endif
    if ($class != "person" && $classes != "all") then
	/bin/rm -f $DB-$class-values.csv $DB-$class-intervals.csv $DB-$class.csv $DB-$class.json
    endif
end

#
# process "all" results into <DB>-all-values and <DB>-all-intervals
#
if ($classes == "all") then
    echo "STAT: creating $DB-$classes-values.csv $DB-$classes-intervals.csv"
    /bin/rm -f $DB-$classes-values.csv $DB-$classes-intervals.csv
    @ j = 1
    echo "STAT: classifiers ($#CLASSIFIERS)"
    foreach i ( $CLASSIFIERS )
	if ($i == \"\") continue;
	if ($i == "NO_TAGS") continue;
	if (! -e "$DB-$i-values.csv") continue;
	if (! -e "$DB-$i-intervals.csv") continue;
        echo -n "."
        if ($j == 1) then
	    cat $DB-$i-values.csv >! $DB-$classes-values.csv
	    cat $DB-$i-intervals.csv >! $DB-$classes-intervals.csv
        else
	    tail +2 $DB-$i-values.csv >> $DB-$classes-values.csv
	    tail +2 $DB-$i-intervals.csv >> $DB-$classes-intervals.csv
	endif
	if ($class != "person") then
	    /bin/rm -f $DB-$i-values.csv $DB-$i-intervals.csv $DB-$i.csv $DB-$i.json
	endif
	@ j++
    end
    echo
endif
