#!/bin/csh -fb

if (! -e /usr/local/bin/brew) then
   echo "*** NO HOME BREW?  Install from http://brew.sh" >& /dev/stderr
   exit
endif
if (! -e /usr/local/bin/jq) then
   echo "*** BREWING 'jq' ..." >& /dev/stderr
   brew install jq >& /dev/stderr
endif

if ($#argv > 0) then
    @ i = 0
    set t = "$argv[1]"
    if (($#t == 1) && ($#argv > 1)) then
	if ($t == "-n") then
	    set model = $argv[2]
	    @ i = 2
	endif
    endif
    if ($#argv > $i) then
	@ i++
        set IMAGE_FILE = "$argv[$i-]"
    endif
endif
if ($?IMAGE_FILE == 0) set IMAGE_FILE = ""

set creds = ~$USER/.watson.visual-recognition.json
set noglob
if (-e $creds) then
    set api_key = ( `jq '.[0]|.credentials.api_key' $creds | sed 's/"//g'` )
    echo "--- USING APIKEY $api_key" >& /dev/stderr
    set url = ( `jq '.[0]|.credentials.url' $creds | sed 's/"//g'` )
    echo "--- USING URL $url" >& /dev/stderr
    # set base
    set TU = $url
else if ($?TU == 0) then
    echo "*** NO CREDENTIALS ($creds); create file and copy credentials from visual-recognition service on bluemix.net" >& /dev/stderr
    exit
endif
unset noglob

if ($?verid == 0) set verid = "v3"
if ($?vdate == 0) set vdate = "2016-05-20"

if ($?model) then
    set classifiers = ( `curl -q -s -L -H "Accept-Language: en" "$TU/$verid/classifiers?api_key=$api_key&version=$vdate" | jq '.classifiers[]|select(.name=="'"$model"'")'` )
else
    set classifiers = ( `curl -q -s -L -H "Accept-Language: en" "$TU/$verid/classifiers?api_key=$api_key&version=$vdate" | jq '.classifiers[]'` )
endif
if ($#classifiers > 0) then
    set classifier = `echo "$classifiers" | jq '.classifier_id' | sed 's/"//g'`
    if ($#classifier > 0) then
	echo "+++ using classifier ($classifier) +++" 
	echo '{ "classifier_ids": ["'$classifier'"] }' >! /tmp/$0:t-params.$$.json
    else
	unset classifier
    endif
else
    echo "+++ no custom classifier (using default) +++" 
endif

foreach ifile ( $IMAGE_FILE )
    if (-s "$ifile") then
	echo "+++ CLASSIFIERS +++"
        if ($?classifier) then
	    set c = `echo "$classifier" | sed "s/ /,/g"`
	    curl -v -L -o /tmp/$0:t.$$.json "$TU/$verid/classify?api_key=$api_key&classifier_ids=default,$c&threshold=0.000001&version=$vdate" \
		-F "images_file=@$ifile"
	else
	    curl -v -L -o /tmp/$0:t.$$.json "$TU/$verid/classify?api_key=$api_key&classifier_ids=default&threshold=0.000001&version=$vdate" \
		-F "images_file=@$ifile"
	endif
	jq '.' /tmp/$0:t.$$.json
	if ($?FACES) then
	    echo "+++ FACES +++"
	    curl -s -q -L -o /tmp/$0:t.$$.json -X POST -F "images_file=@$ifile" -H "Accept-Language: en" "$TU/$verid/detect_faces?api_key=$api_key&version=$vdate"
	    jq '.' /tmp/$0:t.$$.json
	endif
	if ($?TEXT) then
	    echo "+++ TEXT +++"
	    curl -s -q -L -o /tmp/$0:t.$$.json -X POST -F "images_file=@$ifile" -H "Accept-Language: en" "$TU/$verid/recognize_text?api_key=$api_key&version=$vdate"
	    jq '.' /tmp/$0:t.$$.json
	endif
    else
	echo "*** bad image ($ifile)" >& /dev/stderr
    endif
end

cleanup:
rm -f /tmp/$0:t.$$.json /tmp/$0:t-params.$$.json
