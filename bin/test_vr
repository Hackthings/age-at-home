#!/bin/csh -fb

if (! -e /usr/local/bin/brew) then
   echo "*** NO HOME BREW?  Install from http://brew.sh" >& /dev/stderr
   exit
endif
if (! -e /usr/local/bin/jq) then
   echo "*** BREWING 'jq' ..." >& /dev/stderr
   brew install jq >& /dev/stderr
endif

if ($#argv > 0) then
    @ i = 0
    set t = "$argv[1]"
    if (($#t == 1) && ($#argv > 1)) then
	if ("$t" == "-m") then
	    set model = $argv[2]
	    @ i = 2
	endif
    endif
    if ($#argv > $i) then
	@ i++
        set jpegfiles = ( $argv[$i-] ) 
    endif
endif
if ($?jpegfiles == 0) then
    echo "*** PLEASE SPECIFY FILES TO BE CLASSIFIED ***"
    goto done
endif

set creds = ~$USER/.watson.visual-recognition.json
if (-e $creds) then
    set api_key = ( `jq '.[0]|.credentials.api_key' $creds | sed 's/"//g'` )
    echo "--- USING APIKEY $api_key" >& /dev/stderr
    set url = ( `jq '.[0]|.credentials.url' $creds | sed 's/"//g'` )
    echo "--- USING URL $url" >& /dev/stderr
    # set base
    set TU = $url
else if ($?TU == 0) then
    echo "*** NO CREDENTIALS ($creds); create file and copy credentials from visual-recognition service on bluemix.net" >& /dev/stderr
    goto done
endif

if ($?verid == 0) set verid = "v3"
if ($?vdate == 0) set vdate = "2016-05-20"

if ($?model) then
    set classifiers = ( `curl -q -s -L "$TU/$verid/classifiers?api_key=$api_key&version=$vdate" | jq '.classifiers[]|select(.classifier_id=="'"$model"'")'` )
else
    set classifiers = ( `curl -q -s -L "$TU/$verid/classifiers?api_key=$api_key&version=$vdate" | jq '.classifiers[]'` )
endif
if ($#classifiers > 0) then
    set classifier = `echo "$classifiers" | jq '.classifier_id' | sed 's/"//g'`
    if ($#classifier > 0) then
	echo "+++ using classifier ($classifier) +++" 
    else
	unset classifier
    endif
else
    echo "+++ no custom classifier (using default) +++" 
endif

if ($?classifier) then
    set classifier = `echo "$classifier" | sed "s/ /,/g"`
endif

@ i = 1
@ z = 10
@ t = 0
@ n = $#jpegfiles

while ($i <= $n) 
    @ t = $t + $z

    if ($t >= $n) @ t = $n
    set ifiles = ()
    while ($i <= $t)
	set ifile = ( $jpegfiles[$i] )
	if (-s $ifile) then
	    echo "*** ADDING $i $ifile ***"
	    set ifiles = ( $ifiles $ifile )
	else
	    echo "*** FILE ($ifile) does not exist? ***"
	endif
	@ i++
    end
    set nfiles = $#ifiles
    if ($nfiles > 1) then
	zip -q -j -r -u /tmp/$0:t.$$.zip $ifiles
	set ifiles = /tmp/$0:t.$$.zip
    endif

    echo -n "+++ CLASSIFY $nfiles IMAGES "
    set start = `date +%s`
    if ($?classifier) then
    	if ($?USE_DEFAULT) then
	    curl -s -q -L -F "images_file=@$ifiles" \
		"$TU/$verid/classify?api_key=$api_key&classifier_ids=default,$classifier&threshold=0.000001&version=$vdate" >! /tmp/$0:t.$$.json.$$
	else
	    curl -s -q -L -F "images_file=@$ifiles" \
		"$TU/$verid/classify?api_key=$api_key&classifier_ids=$classifier&threshold=0.000001&version=$vdate" >! /tmp/$0:t.$$.json.$$
	endif
    else
	curl -s -q -L -F "images_file=@$ifiles" \
	    "$TU/$verid/classify?api_key=$api_key&classifier_ids=default&threshold=0.000001&version=$vdate" >! /tmp/$0:r.$$.json.$$
    endif
    if ($nfiles > 1) then
	rm -f /tmp/$0:t.$$.zip
	cat /tmp/$0:t.$$.json.$$ | sed "s@$ifiles:t/@@g" >>! /tmp/$0:t.$$.json
    else
	cat /tmp/$0:t.$$.json.$$ >>! /tmp/$0:t.$$.json
    endif
    set end = `date +%s`
    @ elapsed = $end - $start
    echo "($elapsed seconds) +++"

    # should pick only those classified as "person"
    if ($?FACES) then
	echo "+++ FACES +++"
	curl -s -q -L -o /tmp/$0:t.$$.json -X POST -F "images_file=@$ifile" "$TU/$verid/detect_faces?api_key=$api_key&version=$vdate"
	jq '.' /tmp/$0:t.$$.json
    endif
    if ($?TEXT) then
	echo "+++ TEXT +++"
	curl -s -q -L -o /tmp/$0:t.$$.json -X POST -F "images_file=@$ifile" "$TU/$verid/recognize_text?api_key=$api_key&version=$vdate"
	jq '.' /tmp/$0:t.$$.json
    endif
end

output:

# top class (by score) per image for each classifier applied
# {"image":"20160428095504-749-00.jpg","classes":[{"name":"default","id":"default","classes":null},{"name":"damp-cloud","id":"dampcloud_231362639","classes":{"class":"person","score":0.309657}},{"name":"damp-cloud","id":"dampcloud_257127137","classes":{"class":"person","score":0.310192}},{"name":"rough-fog","id":"roughfog_1514965218","classes":{"class":"dog","score":0.480892}},{"name":"rough-fog","id":"roughfog_228618634","classes":{"class":"dog","score":0.478403}},{"name":"rough-fog","id":"roughfog_653635843","classes":{"class":"dog","score":0.479636}},{"name":"rough-fog","id":"roughfog_80528242","classes":{"class":"dog","score":0.512194}}]}
# jq -c '.images[]|{"image":.image,"classes":[.classifiers[]|{"name":.name,"id":.classifier_id,"classes":.classes|sort_by(.score)[-1]}]}'

# all classes per image for all classifiers applied
# {"image":"20160428095504-749-00.jpg","classes":[{"class":"person","score":0.309657},{"class":"person","score":0.310192},{"class":"cat","score":0.0383389},{"class":"dog","score":0.480892},{"class":"person","score":0.186817},{"class":"cat","score":0.0383551},{"class":"dog","score":0.478403},{"class":"person","score":0.186166},{"class":"cat","score":0.0383465},{"class":"dog","score":0.479636},{"class":"person","score":0.186099},{"class":"cat","score":0.0321449},{"class":"dog","score":0.512194},{"class":"person","score":0.142043}]}
# jq -c '.images[]|{"image":.image,"classes":[.classifiers[].classes[]]}' 

# top class (by score) per image for all classifiers applied
# {"image":"20160428095504-749-00.jpg","classes":{"class":"dog","score":0.512194}}

jq -c '.images[]|{"image":.image,"classes":[.classifiers[].classes[]]|sort_by(.score)[-1]}' /tmp/$0:t.$$.json


done:

# rm -f /tmp/$0:t.$$.json
